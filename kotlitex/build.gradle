apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'maven-publish'

def  version = '1.0'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName(version)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // for workaround https://github.com/Kotlin/kotlinx.coroutines/issues/1059
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        String testCommon = "src/testCommon/java"
        test {
            java.srcDir testCommon
        }
        androidTest {
            java.srcDir testCommon
        }
    }

}

publishing {
    publications {
        mavenJAR(MavenPublication) {
            afterEvaluate {
               artifact libraryJar
            }

            pom {
                packaging = 'jar'

                name = 'KotliTeX'
                description = 'KotliTeX is KaTeX\'s Android port, written in Kotlin.'
                url = 'https://github.com/oppia/kotlitex'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/oppia/kotlitex/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-RC"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'

}

task libraryJar(type: Jar, dependsOn: 'compileReleaseKotlin') {
    from {
        compileReleaseKotlin.destinationDir
    }
    android.sourceSets.all { sourceSet ->
        from sourceSet.assets.srcDirs
    }

    archiveName "kotlitex-${version}.jar"
}
